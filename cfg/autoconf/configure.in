dnl Process this file with autoconf to produce a configure script.

dnl Disabling cache file - now commented and not used
dnl define([AC_CACHE_LOAD], )dnl
dnl define([AC_CACHE_SAVE], )dnl

dnl Default C source file
AC_INIT(../../gammu/gammu.c)

dnl Default prefix for package
AC_PREFIX_DEFAULT("/usr/local")

dnl Version
VERSION=`cat VERSION`
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_SUBST(VERSION)

dnl Compiler flags
dnl -Wno-format-y2k is against "%c" format warning. The only problem is that
dnl this doesn't remove warning with gcc 2.95, but for 3.0 and 3.2 it works as
dnl expected.
if test x$CFLAGS = x ; then
    CFLAGS="-O2 -Wall -Wno-format-y2k"
else
    CFLAGS="$CFLAGS -Wno-format-y2k"
fi

dnl Checks for programs.
AC_PATH_PROG(RM, rm, no)
AC_PATH_PROG(INSTALL, install, no)
AC_PATH_PROG(FIND, find, no)

dnl Checks for libraries.

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(scandir alphasort iswspace towlower)

dnl Now this is checking is not used
dnl AC_CHECK_FUNCS(mktime select socket strdup strerror strstr)
dnl AC_SUBST(HAVE_SCANDIR)
dnl AC_SUBST(HAVE_ALPHASORT)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(dirent.h mysql/mysql.h)
dnl This checking is not used now
dnl AC_CHECK_HEADERS(fcntl.h sys/file.h sys/ioctl.h sys/time.h unistd.h)
dnl AC_SUBST(HAVE_DIRENT_H)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl doc directory
DOC_DIR="$datadir/doc/gammu"
AC_ARG_WITH(docdir,
	[  --with-docdir=DIR       Specifies the documentation directory],
	[ if test x$withval = xyes; then
		AC_MSG_WARN(Usage is: --with-docdir=DIR)
	  else
		DOC_DIR="$withval"
	  fi
	]
)
AC_SUBST(DOC_DIR)

dnl locales directory
LOCALE_DIR="$datadir/gammu"
AC_ARG_WITH(localedir,
	[  --with-localedir=DIR       Specifies the localisation directory],
	[ if test x$withval = xyes; then
		AC_MSG_WARN(Usage is: --with-localedir=DIR)
	  else
		LOCALE_DIR="$withval"
	  fi
	]
)
AC_SUBST(LOCALE_DIR)

dnl Path for making RPM
RPM_DIR="/usr/src/RPM"
AC_ARG_WITH(rpmdir,
	[  --with-rpmdir=DIR       Specifies the directory for making RPM],
	[ if test x$withval = xyes; then
		AC_MSG_WARN(Usage is: --with-rpmdir=DIR)
	  else
		RPM_DIR="$withval"
	  fi
	]
)
AC_SUBST(RPM_DIR)

dnl temp directory
TEMP_DIR="/tmp"
AC_ARG_WITH(tmpdir,
	[  --with-tmpdir=DIR       Specifies the temporary directory],
	[ if test x$withval = xyes; then
		AC_MSG_WARN(Usage is: --with-tmpdir=DIR)
	  else
		TEMP_DIR="$withval"
	  fi
	]
)
AC_SUBST(TEMP_DIR)

dnl Path for bluetooth library
gsm_bt_dir=""
AC_ARG_WITH(bluedir,
	[  --with-bluedir=DIR      Specifies the base libbluetooth],
	[ if test x$withval = xyes; then
		AC_MSG_WARN(Usage is: --with-bluedir=DIR)
	  else
		LDLIBS="$LDLIBS -L$withval/lib/ -lbluetooth"
		CFLAGS="$CFLAGS -I$withval/include/"
		gsm_bt_dir="$withval"
	  fi
	]
)

dnl Enabling or not protocols
AC_ARG_ENABLE(mbus,
	[  --disable-mbus          Disable Nokia MBUS2 protocol],
	[gsm_enable_mbus2="no"],
	[AC_DEFINE(GSM_ENABLE_MBUS2) gsm_enable_mbus2="yes"])
AC_ARG_ENABLE(fbus,
	[  --disable-fbus          Disable Nokia FBUS2 protocol],
	[gsm_enable_fbus2="no"],
	[AC_DEFINE(GSM_ENABLE_FBUS2) gsm_enable_fbus2="yes"])
AC_ARG_ENABLE(fbusdlr3,
	[  --disable-fbusdlr3      Disable Nokia FBUS2 protocol
	                  with DLR3(P) cable (Nokia 6210/63xx/71xx)],
	[gsm_enable_fbus2dlr3="no"],
	[AC_DEFINE(GSM_ENABLE_FBUS2DLR3) gsm_enable_fbus2dlr3="yes"])
AC_ARG_ENABLE(fbusblue,
	[  --disable-fbusblue      Disable Nokia FBUS2 protocol
	                  over Bluetooth legacy device (Nokia 6210)],
	[gsm_enable_fbus2blue="no"],
	[AC_DEFINE(GSM_ENABLE_FBUS2BLUE) gsm_enable_fbus2blue="yes"])
AC_ARG_ENABLE(fbusirda,
	[  --disable-fbusirda      Disable FBUS2 over direct infrared device
	                  (Nokia 5210/61xx/82xx)],
	[gsm_enable_fbus2irda="no"],
	[AC_DEFINE(GSM_ENABLE_FBUS2IRDA) gsm_enable_fbus2irda="yes"])
AC_ARG_ENABLE(phonetblue,
	[  --disable-phonetblue    Disable Nokia PHONET FBUS over Bluetooth
                          legacy device (all new Nokias)],
	[gsm_enable_phonetblue="no"],
	[AC_DEFINE(GSM_ENABLE_PHONETBLUE) gsm_enable_phonetblue="yes"])
AC_ARG_ENABLE(at,
	[  --disable-at            Disable AT protocol],
	[gsm_enable_at="no"],
	[AC_DEFINE(GSM_ENABLE_AT) gsm_enable_at="yes"])
AC_ARG_ENABLE(irdaphonet,
	[  --disable-irdaphonet    Disable NOKIA PHONET FBUS over irda sockets
	                  (all new Nokia)],
	[gsm_enable_irdaphonet="no"],
	[AC_CHECK_HEADER(linux/irda.h,
		[AC_DEFINE(GSM_ENABLE_IRDAPHONET) gsm_enable_irdaphonet="yes"],
		[gsm_enable_irdaphonet="no"])])

AC_CACHE_CHECK([for the Bluez Bluetooth stack], ac_cv_have_sockaddr_rc,
     [AC_TRY_COMPILE([#include <sys/socket.h>
          #include <bluetooth/bluetooth.h>
          #include <bluetooth/rfcomm.h>],
          [struct sockaddr_rc rc;],
           ac_cv_have_sockaddr_rc=yes,
               ac_cv_have_sockaddr_rc=no)])
AC_ARG_ENABLE(bluefbus,
	[  --disable-bluefbus      Disable Nokia FBUS2 over Bluetooth stack
                          (Nokia 6210)],
	[gsm_enable_bluefbus2="no"],
	[if test $ac_cv_have_sockaddr_rc = yes; then
		AC_DEFINE(GSM_ENABLE_BLUEFBUS2)
		gsm_enable_bluefbus2="yes"
		LDLIBS="$LDLIBS -lbluetooth"
	  else
		gsm_enable_bluefbus2="no"
	  fi])
AC_ARG_ENABLE(bluephonet,
	[  --disable-bluephonet    Disable Nokia PHONET FBUS over Bluetooth
                          stack (all new Nokias)],
	[gsm_enable_bluephonet="no"],
	[if test $ac_cv_have_sockaddr_rc = yes; then
		AC_DEFINE(GSM_ENABLE_BLUEPHONET)
		gsm_enable_bluephonet="yes"
		LDLIBS="$LDLIBS -lbluetooth"
	  else
		gsm_enable_bluephonet="no"
	  fi])
AC_ARG_ENABLE(blueat,
	[  --disable-blueat        Disable AT commands over Bluetooth stack],
	[gsm_enable_blueat="no"],
	[if test $ac_cv_have_sockaddr_rc = yes; then
		AC_DEFINE(GSM_ENABLE_BLUEAT)
		gsm_enable_blueat="yes"
		LDLIBS="$LDLIBS -lbluetooth"
	  else
		gsm_enable_blueat="no"
	  fi])	

dnl Some general options
AC_ARG_ENABLE(debug,
	[  --enable-debug          Compile with debug code],
	[AC_DEFINE(DEBUG) debug="yes"],
	[debug="no"])
AC_ARG_ENABLE(cb,
	[  --enable-cb             Enable Cell Broadcast messages support],
	[AC_DEFINE(GSM_ENABLE_CELLBROADCAST) gsm_enable_cellbroadcast="yes"],
	[gsm_enable_cellbroadcast="no"])
AC_ARG_ENABLE(7110incoming,
	[  --enable-7110incoming   Enable Nokia 62xx/71xx/9xxx incoming call/SMS info],
	[AC_DEFINE(GSM_ENABLE_N71_92INCOMINGINFO) gsm_enable_71_92incominginfo="yes"],
	[gsm_enable_71_92incominginfo="no"])
AC_ARG_ENABLE(newcalendar,
	[  --enable-newcalendar    Force using new frames for calendar for DCT4 phones],
	[AC_DEFINE(GSM_FORCE_DCT4_NEW_CALENDAR) gsm_force_dct4_new_calendar="yes"],
	[gsm_force_dct4_new_calendar="no"])
AC_ARG_ENABLE(backup,
	[  --disable-backup        Disable backup/restore functions],
	[gsm_enable_backup="no"],
	[AC_DEFINE(GSM_ENABLE_BACKUP) gsm_enable_backup="yes"])

dnl Phone modules
AC_ARG_ENABLE(alcatel,
	[  --disable-alcatel       Disable Alcatel phone support],
	[gsm_enable_alcatel="no"],
	[AC_DEFINE(GSM_ENABLE_ALCABUS)
	 AC_DEFINE(GSM_ENABLE_ALCATEL) gsm_enable_alcatel="yes"])
AC_ARG_ENABLE(6110,
	[  --disable-6110          Disable Nokia 61xx and compatible support],
	[gsm_enable_nokia6110="no"],
	[AC_DEFINE(GSM_ENABLE_NOKIA6110) gsm_enable_nokia6110="yes"])
AC_ARG_ENABLE(6510,
	[  --disable-6510          Disable Nokia 6510 and compatible support],
	[gsm_enable_nokia6510="no"],
	[AC_DEFINE(GSM_ENABLE_NOKIA6510) gsm_enable_nokia6510="yes"])
AC_ARG_ENABLE(7110,
	[  --disable-7110          Disable Nokia 7110 and compatible support],
	[gsm_enable_nokia7110="no"],
	[AC_DEFINE(GSM_ENABLE_NOKIA7110) gsm_enable_nokia7110="yes"])
AC_ARG_ENABLE(9210,
	[  --disable-9210          Disable Nokia 9210 and compatible support],
	[gsm_enable_nokia9210="no"],
	[AC_DEFINE(GSM_ENABLE_NOKIA9210) gsm_enable_nokia9210="yes"])
AC_ARG_ENABLE(atgen,
	[  --disable-atgen         Disable generic AT phones support],
	[gsm_enable_atgen="no"],
	[AC_DEFINE(GSM_ENABLE_ATGEN) gsm_enable_atgen="yes"])

AC_SUBST(LDLIBS)

dnl Writing output files
AC_CONFIG_HEADER(config.h)
AC_OUTPUT(Makefile.cfg
	  ../rpm/gammu.spec)

dnl Writing info
echo "
  G A M M U (GNU All Mobiles Management Utilities)
  (c) 2002 - 2003 by Marcin Wiacek (www.mwiacek.com)
  Version $VERSION

  Directories
  -----------
  Prefix                                        - $prefix
  Executable prefix                             - $exec_prefix
  
  Man pages                                     - $mandir/man1
  Shared .so library                            - $libdir
  Docs and examples                             - $DOC_DIR
  Localisation files                            - $LOCALE_DIR
  Binaries                                      - $bindir
  RPM making directory                          - $RPM_DIR
  Temporary directory                           - $TEMP_DIR
  Bluetooth library path                        - $gsm_bt_dir
  
  General options
  ---------------
  Debug                                         - $debug
  CB messages                                   - $gsm_enable_cellbroadcast
  Backup functions                              - $gsm_enable_backup

  Phone protocols
  ---------------
  Nokia MBUS2                                   - $gsm_enable_mbus2
  Nokia FBUS2                                   - $gsm_enable_fbus2
  Nokia FBUS2 for DLR3 cable                    - $gsm_enable_fbus2dlr3
  Nokia FBUS2 for Bluetooth legacy device       - $gsm_enable_fbus2blue
  Nokia FBUS2 for direct infrared device        - $gsm_enable_fbus2irda
  Nokia PHONET FBUS for Bluetooth legacy device - $gsm_enable_phonetblue
  AT                                            - $gsm_enable_at
  Alcatel                                       - $gsm_enable_alcatel

  Phone protocols with infrared stack
  -----------------------------------
  Nokia PHONET FBUS                             - $gsm_enable_irdaphonet

  Phone protocols with Bluez Bluetooth stack
  ------------------------------------------
  AT commands                                   - $gsm_enable_blueat
  Nokia FBUS2                                   - $gsm_enable_bluefbus2
  Nokia PHONET FBUS                             - $gsm_enable_bluephonet

  Phone modules
  -------------
  Alcatel                                       - $gsm_enable_alcatel
  AT (Siemens, Alcatel, Nokia, WaveCom, other)  - $gsm_enable_atgen
  Nokia 3210/33xx/3410/51xx/5210/5510/61xx/82xx - $gsm_enable_nokia6110
  Nokia 3510/6310(i)/6510/8310                  - $gsm_enable_nokia6510
           Use always new frames for calendar   - $gsm_force_dct4_new_calendar
  Nokia 62xx/71xx                               - $gsm_enable_nokia7110
           Incoming call/SMS info               - $gsm_enable_71_92incominginfo
  Nokia 92xx                                    - $gsm_enable_nokia9210
           Incoming call/SMS info               - $gsm_enable_71_92incominginfo

  Please use now:
  
  * "make"            to make static version of Gammu
  * "make install"    to install Gammu in your system (root)
  * "make installlib" to install Gammu with library in your system (root)
  * "make makerpm"    to make RPM package
  * "make makedeb"    to make Debian package
"
